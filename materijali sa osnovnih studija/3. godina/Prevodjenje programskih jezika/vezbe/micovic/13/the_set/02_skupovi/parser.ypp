%{
#include <iostream>
#include <string>
#include <map>
#include <cstdlib>
#include "the_set.hpp"

#define YYDEBUG 1

extern int yylex();

std::map<std::string, the_set*> tablica;

// U jeziku C++ umesto NULL mozemo da koristimo nullptr
// koji oznacava NULL pokazivac. U C-u je NULL najcesce samo
// makro poput #define NULL 0...
// Ako univerzalni skup nije definisan, bice postavljen na nullptr.
the_set* universal_set = nullptr;

void yyerror(std::string msg)
{
    std::cerr << "Syntax error: " << msg << std::endl;
    exit(EXIT_FAILURE);
}
%}

%union {
    std::string* str_type;
    int int_type;
    the_set* set_type;
}

%token <str_type> id_token
%token print_token fromto_token union_token intersect_token check_token card_token
%token <int_type> num_token
%type <set_type> Skup Niz E

%left '~'
%left '\\'
%left intersect_token;
%left union_token;

%%
Program
    : Program Naredba
    | Naredba
    ;

Naredba
    : id_token '=' E ';' {
        tablica[*$1] = $3;
        delete $1;
    } | print_token E ';' {
        std::cout << *$2 << std::endl;
        delete $2;
    } | check_token num_token ':' E ';' {
        bool b = $4->contains($2);
        std::cout << (b ? "True" : "False") << std::endl;
        delete $4;
    } | check_token E '<' E ';' {
        bool b = $2->subset(*$4);
        std::cout << (b ? "True" : "False") << std::endl;
        delete $2;
        delete $4;
    } | card_token E ';' {
        unsigned card = $2->card();
        std::cout << card << std::endl;
        delete $2;
    }
    ;

Skup
    /* Inicijalizacija {1, 2, 3} */
    : '{' Niz '}' {
        $$ = $2;
    }
    /* Inicijalizacija {} */
    | '{' '}' {
        $$ = new the_set();
    }
    /* Inicijalizacija 7..10 */
    | num_token fromto_token num_token {
        $$ = new the_set();
        $$->init($1, $3);
    }
    ;

Niz
    : Niz ',' num_token {
        $$ = $1;
        $$->add($3);
    }
    | num_token {
        $$ = new the_set();
        $$->add($1);
    }
    ;

E
    : E union_token E {
        // Posto union_op prihvata const the_set& dereferenciramo $3.
        $$ = $1->union_op(*$3);
        delete $1;
        delete $3;
    } | E intersect_token E {
        $$ = $1->intersect_op(*$3);
        delete $1;
        delete $3;
    } | E '\\' E {
        $$ = $1->diff_op(*$3);
        delete $1;
        delete $3;
    } | Skup {
        $$ = $1;
    } | id_token {
        auto finder = tablica.find(*$1);
        if (finder != tablica.end()) {
            // Pozivamo konstruktor kopije
            // Nismo ga implementirali, ali ga C++ generise za nas.
            $$ = new the_set(*finder->second);
        } else {
            std::cout << *$1 << " nije definisana!" << std::endl;
            $$ = new the_set();
        }
        delete $1;
    } | '~' E {
        // Proveravamo da li je definisan univerzalni skup
        auto finder = tablica.find("UniversalSet");
        if (finder == tablica.end()) {
            // Nije definisan skup, prijavljujemo gresku
            std::cout << "Nije definisan univerzalni skup" << std::endl;
            // Ukoliko ne prekidamo program vec zelimo da nastavimo rad,
            // mozemo postaviti $$ na $2 (sto u principu nije tacno) ali
            // omogucava da program nastavi rad.
            $$ = $2;
        } else {
            // Izvlacimo pokazivac na univerzalni skup
            the_set* univ_set = finder->second;
            // Izracunavamo komplementarni skup
            the_set* complement_set = $2->complement(*univ_set);
            $$ = complement_set;

            // Dealociramo $2 jer vise nije potreban.
            delete $2;
        }
    }
    ;
%%
int main()
{
    yyparse();
    return 0;
}
